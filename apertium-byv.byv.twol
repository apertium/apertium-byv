Alphabet
 a b c ç d ɛ e ə f g h i ı j k l m n ŋ o ɔ p q r s t u v w x y z ɑ ʉ ʼ ! α ε
 A B C Ç D Ɛ E Ə F G H   I J K L M N Ŋ O Ɔ P Q R S T U V W X Y Z Ɑ Ʉ ʼ ! Α Ε

̀
́
̂
̌

 %{ˆ%}:̂       ! Ton descendent
 %{ˇ%}:̌       ! Ton montant
 %{ˋ%}:̀       ! Ton bas
 %{ˊ%}:0      ! Ton haut

 %{↘%}:0      !
 %{↗%}:0      !
 %{↓%}:0      !


 %{N%}:n      ! 
 %{ə%}:ə      ! 

 %>:0  ;

Sets

Vow =   a e ɛ ə i ı o ɔ u ɑ ʉ
        A E ɛ Ə I I O Ɔ U Ɑ Ʉ   ;

Cns =   b c ç d f g h j k l m n ŋ p q r s t v w x y z ʼ
        B C Ç D F G H J K L M N Ŋ P Q R S T V W X Y Z   ;

Rules 

!"Tone in possessive"
!Tx:Ty <=> _ (:Cns) %>: %{↘%}: ; 
!      where Tx in ( %{ˋ%}  %{ˊ%}  )
!            Ty in (   ̌       ̂     ) 
!      matched;

"High to low tone"
Tx:Ty <=> _ (:Cns) %>: %{↓%}: ; 
      where Tx in (   %{ˊ%}  )
            Ty in (      ̀    ) 
      matched;

! {N}:m >:0 b ɛ {ˊ}: n:n >:0 {↓}:0

!"Falling tone"
!Tx:Ty <=> _ (:Cns) %>: %{↗%}: ; 
!      where Tx in (   %{ˋ%}   )
!            Ty in (    ̂     ) 
!      matched;

"Nasal assimilation"
%{N%}:m <=> _ (%{ˊ%}:) %>: [ b | v | f ] ;

"Infinitive mark assimilation"
%{ə%}:0 <=> :Vow [ %{ˋ%}: | %{ˊ%}: ] %>: _ ;

"Infinitive tone assimilation"
Tx:Ty <=> _ %>: %{ə%}:  ;
      except 
     %{N%}: %>: %{ˊ%}: %>: Cns: Vow: _  %>: %{ə%}: ;
      where Tx in ( %{ˋ%}  ) 
            Ty in (   ̌     )
      matched ;

! z i {ˊ}:0 >:0 {ə}:0 {ˊ}:0 
! k ə {ˊ}:0 b >:0 {ə}:ə {ˊ}:0 

!kəbə<v><inf>:kə{ˊ}b>{ə}{ˊ}
!làbə<v><inf>:la{ˋ}b>{ə}{ˊ}
!zi<v><inf>:zi{ˊ}>{ə}{ˊ}
!zwǐ<v><inf>:zwi{ˋ}>{ə}{ˊ}

"Floating high tone in past I"
Tx:Ty <=> %{N%}: %>: %{ˊ%}: %>: Cns: Vow: _  ;
      except
                                          _ %>: %{ə%}:  ;
          %{N%}: %>: %{ˊ%}: %>: Cns: Vow: _ (Cns:) %>: %{ə%}: ;
      where Tx in ( %{ˋ%}  ) 
            Ty in (   ̂   )
      matched ;

! {N}:n >:0 {ˊ}:0 >:0 l:d a {ˋ}: b


"B -> H in past IV"
%{ˋ%}:0 <=> %{N%}: %>: %{ˊ%}: %>: Cns: Vow: _ (Cns:)  %>: %{ə%}: ;

! {N}:n >:0 {ˊ}:0 >:0 l:d a {ˋ}:0 b >:0 {ə}:ə {ˊ}:0

! {N}>{ˊ}>la{ˋ}b>{ə}{ˊ}

"Rule 1: Better name?"
Cx:Cy <=>             %{N%}: %>: _ ;
          %{N%}: %>: %{ˊ%}:  %>: _ ;
      where Cx in (  l  y ) 
            Cy in (  d  j ) 
      matched ;    

"Rule 2: Better name?"
h:0 <=> %{N%}: %>: g _  ;
